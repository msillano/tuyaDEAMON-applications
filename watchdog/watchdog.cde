%timerPDM02,(C)2020 m.sillano, CC:BY+NC+SA license
%CMD>script http://192.168.1.2:88/sonoff/timerPDM02.cde

config ntp_server time.google.com
config ntp_timezone 2
config @6 /ozone/timerpdm/1

on init
do
setvar $tmp=(@8*1)
setvar @8=($tmp+1)
println **start:|@8
setvar $oo=0
setvar $m=60000
if $tmp=0 then
 setvar @1=0
 setvar @2=5
 setvar @3="00:00:00"
 setvar @4=100
 setvar @5=0
 setvar @7=5
else 
 if @1>0 then
 setvar $oo=1
 gpio_out 12 1
 settimer 1 $m
 if 100>@4 then
 settimer 3 (@4*603)
 endif
 endif
endif
setvar $t=0
setvar $tz=2
setvar $cmd=@6|/command
setvar $set=@6|/set
settimer 2 1000
setalarm 1 @3
subscribe local $cmd
subscribe local $set

on mqttconnect
do
subscribe remote $cmd
subscribe remote $set

on topic remote $set
do
publish local $set $this_data

on topic remote $cmd
do
publish local $cmd $this_data

on topic local $set
do
setvar $tmp=json_parse(data,$this_data)
setvar $v=json_parse(value,$this_data)
println "**set "|$tmp|" "|$v
if $tmp=ssid then
 system "set ssid "|$v
 system "set auto_connect 0"
 system save
 system reset
endif

if $tmp=pssw then
 system "set password "|$v
 system "set auto_connect 1"
 system save
 system reset
endif

if $tmp=timezone then
 setvar $tz=$v
 system "set ntp_timezone "|$v
 system save
endif

if $tmp=nowtime then
 system "set ntp_time "|$v
 setvar $tz=0
 system "set ntp_timezone 0"
endif

if $tmp=mode then
 if @5>0 then
 setvar @5=0
 else 
 setvar @5=$v
 endif
 publish local $cmd off
endif

if $tmp=PDM then
 if $v>2 then
 setvar @4=$v
 else
 setvar @4=2
 endif
 if 100>$v then
 settimer 3 $v*603
 endif
endif

if $tmp=ONtime then
 setvar @2=$v
endif

if $tmp=OFFtime then
 setvar @7=$v
endif

if $tmp=Ttime then
 setvar @3=$v
 setalarm 1 $v
endif

setvar $tmp=@6|/status/config
publish local $tmp "{\"mode\":"|@5|",\"ONtime\":"|@2|",\"OFFtime\":"|@7|",\"PDM\":"|@4|",\"Ttime\":\""|@3|"\",\"timezone\":"|$tz|"}"

on topic local $cmd
do
setvar $ol=$oo
setvar $v=$this_data

if ($v=go)*(@5>0) then
 setvar $oo=1
 setvar @1=@2
 settimer 1 $m
endif

if $v=end then
 setvar @5=0
 publish local $set send
endif

if $v=off then
 setvar $oo=0
endif

if @5=0 then
 setvar $oo=0
 gpio_out 12 0
 setvar @1=0
 settimer 4 1
 settimer 1 2
else
 if not($ol=$oo) then
 gpio_out 12 $oo
 if ($oo=1)*(100>@4) then
 settimer 3 (@4*603)
 endif
 endif
endif
setvar $tmp=@6|/status/output
publish local $tmp "{\"Time\":\""|$timestamp|"\",\"mode\":"|@5|",\"PDM\":"|@4|",\"output\":"|$oo|",\"count\":"|@1|",\"toggle\":"|$t|"}"

on gpio_interrupt 0 pullup
do
if $this_gpio=0 then
 if @5>0 then
 if (@1=0)*($t=0) then
 settimer 4 $m
 setvar $t=60
 gpio_out 13 0
 publish local $cmd send
 else
 publish local $cmd end
 endif
 else
 setvar @5=1
 publish local $set send
 endif
endif

on timer 1
do
if @1>0 then
 setvar @1=(@1-1)
 settimer 1 $m
endif
if not(@1>0) then
 if @5=1 then
 setalarm 1 @3
 publish local $cmd end
 endif
 if @5=2 then
 if $oo=1 then
 setvar @1=@7
 settimer 1 $m
 publish local $cmd off
 else
 publish local $cmd go
 endif
 endif
endif

on timer 2
do
setvar $mg=$timestamp
if @5=0 then
 setvar $mg=$mg|" Standby: waiting MODE"
else
 if @5=1 then
 setvar $mg=$mg|" Auto: "
 endif
 if @5=2 then
 setvar $mg=$mg|" Loop: "
 endif
 if $oo=1 then
 setvar $mg=$mg|ON\ |@4|"% ("|@1|"/"|@2|")"
 else
 if $t>0 then
 setvar $t=$t-1
 setvar $mg=$mg|$t|"\\\" to START"
 else
 if (@5=2)*(@1>0) then
 setvar $mg=$mg|"OFF ("|@1|"/"|@7|")"
 else 
 if @5=1 then
 setvar $mg=$mg|waiting\ |@3
 else
 setvar $mg=$mg|waiting\ START
 endif
 endif
 endif
 endif
endif
settimer 2 1000
setvar $tmp=@6|/status/info
gpio_out 13 ((@5=0)*not(gpio_in(13)))
publish local $tmp "[\""|$mg|"\","|(not(gpio_in(13))+gpio_in(12))|"]"

on timer 3
do
if $oo=1 then
 if gpio_in(12)=1 then
 gpio_out 12 0
 settimer 3 (100-@4)*603
 else
 gpio_out 12 1
 settimer 3 @4*603
 endif
endif

on timer 4
do
 setvar $t=0
 if @5>0 then
 publish local $cmd go
 endif

on alarm 1
do
if (@5=1)*($oo=0) then
 setvar $t=0
 publish local $cmd go
endif
